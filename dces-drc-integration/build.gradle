import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
	id "org.sonarqube" version "5.1.0.4882"
}

def versions = [
		pitestVersion          : '1.16.1',
		sentryVersion          : '7.13.0',
		springdocVersion       : '2.1.0',
		okhttpVersion          : '4.12.0',
		mockwebserverVersion   : '4.12.0',
		reactorTestVersion     : '3.2.3',
		springSecurityConfig   : '6.3.1',
		springSecurityWeb      : '6.3.1',
		springOAuthJose        : '6.3.1',
		springOAuthClient      : '3.3.2',
		springOAuthResource    : '3.3.2',
		springauthserverVersion: '1.0.1',
		jjwtVersion            : '0.11.5',
		jaxbXjc                : '4.0.5',
		jaxbImpl               : '4.0.5',
		jaxbCore               : '4.0.5',
		jakartaActivation      : '2.0.1',
		jakartaXmlBindApi      : '4.0.2',
		glassfishJaxb          : '4.0.5',
		resilience4jVersion    : '2.2.0',
		wiremockVersion        : '3.9.1',
		notifyVersion          : '4.1.0-RELEASE',
		micrometerio           : '1.3.2',
		prometheus             : '1.13.2',
		logbackEncoderVersion  : '7.2',
		serenityVersion        : "4.1.10",
		postgresqlVersion      : "42.7.4"
]

group = 'uk.gov.justice.laa.crime'

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

project.ext {
	jaxbTargetDir = file("src/main/java")
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	xsd2java
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestImplementation.extendsFrom testCompile
	integrationTestImplementation.extendsFrom testRuntime
	integrationTestCompileOnly {
		extendsFrom testCompileOnly
	}
	integrationTestAnnotationProcessor {
		extendsFrom testAnnotationProcessor
	}
	integrationTestRuntimeOnly {
		extendsFrom testRuntimeOnly
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Import Maven bills-of-materials (BOM) files as Gradle platforms:
	implementation platform(SpringBootPlugin.BOM_COORDINATES)
	implementation platform("io.sentry:sentry-bom:$versions.sentryVersion")

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// oAuth
	implementation "org.springframework.security:spring-security-config"
	implementation "org.springframework.security:spring-security-web"
	implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
	implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
	implementation "org.springframework.security:spring-security-oauth2-jose"

	implementation 'io.sentry:sentry-spring-boot-starter-jakarta'
	runtimeOnly "org.postgresql:postgresql:$versions.postgresqlVersion"
	implementation "org.flywaydb:flyway-core:6.4.3"


	implementation 'io.sentry:sentry-logback'

	implementation "net.logstash.logback:logstash-logback-encoder:$versions.logbackEncoderVersion"
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$versions.springdocVersion"
	implementation "io.github.resilience4j:resilience4j-spring-boot3:$versions.resilience4jVersion"

	// XML Conversion
	implementation "org.glassfish.jaxb:jaxb-runtime:$versions.glassfishJaxb"
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	xsd2java "com.sun.xml.bind:jaxb-xjc:$versions.jaxbXjc"
	xsd2java "com.sun.xml.bind:jaxb-impl:$versions.jaxbImpl"
	xsd2java "com.sun.xml.bind:jaxb-core:$versions.jaxbCore"
	xsd2java "com.sun.activation:jakarta.activation:$versions.jakartaActivation"
	xsd2java "jakarta.xml.bind:jakarta.xml.bind-api:$versions.jakartaXmlBindApi"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	implementation "io.micrometer:micrometer-tracing-bom:$versions.micrometerio"
	implementation "io.micrometer:micrometer-tracing-bridge-brave:$versions.micrometerio"
	implementation "io.micrometer:micrometer-registry-prometheus:$versions.prometheus"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.springframework.security:spring-security-test"
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation "org.wiremock:wiremock-standalone:$versions.wiremockVersion"
	testImplementation "com.squareup.okhttp3:mockwebserver:$versions.mockwebserverVersion"
	testImplementation "org.flywaydb:flyway-core:6.4.3"

	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	testImplementation "net.serenity-bdd:serenity-rest-assured:$versions.serenityVersion"

	implementation 'net.datafaker:datafaker:2.4.0'

}

jacoco {
	toolVersion = "0.8.8"
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

sourceSets {
	// Note that just declaring this sourceset creates two configurations.
	integrationTest {
		java {
			compileClasspath += main.output
			runtimeClasspath += main.output
		}
	}
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'**/entity/**',
					'**/dto/**',
					'**/config/**'
			])
		}))
	}
}

sonar {
	properties {
		property "sonar.projectName", "laa-dces-drc-integration"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.organization", "ministryofjustice"
		property "sonar.projectKey", "ministryofjustice_laa-dces-drc-integration"
		property "sonar.exclusions", "**/entity/**.java , **/model/**.java , **/dto/**.java , **/config/**.java, **/jms/**.java,**/exception/**.java,**/handler/**.java,**/maatapi/**Client**.java,**/generated/**, **/listener/stub/DrcStubRestController.java, src/test/**"
		property "sonar.coverage.exclusions", "**/DcesDrcIntegrationApplication.java, **/controller/StubAckFromDrcController.java, **/controller/TempTestController.java"
		property "sonar.coverage.jacoco.xmlReportPaths",
				"${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
	}
}

task xsd2java() {
	doLast {
		jaxbTargetDir.mkdirs()

		ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.xsd2java.asPath)
		ant.jaxbTargetDir = jaxbTargetDir

		ant.xjc(
				destdir: '${jaxbTargetDir}',
				package: 'uk.gov.justice.laa.crime.dces.integration.model.generated.fdc',
				schema: 'src/main/resources/schemas/fdc.xsd'
		)

		ant.xjc(
				destdir: '${jaxbTargetDir}',
				package: 'uk.gov.justice.laa.crime.dces.integration.model.generated.contributions',
				schema: 'src/main/resources/schemas/contributions.xsd'
		)

		ant.xjc(
				destdir: '${jaxbTargetDir}',
				package: 'uk.gov.justice.laa.crime.dces.integration.model.generated.ack',
				schema: 'src/main/resources/schemas/fileACK.xsd'
		)
	}
}

task integrationTest(type: Test) {
	useJUnitPlatform()
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
}

compileJava.dependsOn xsd2java